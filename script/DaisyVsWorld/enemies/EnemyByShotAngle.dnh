#include"script/default_system/Default_ShotConst.txt"
#include"script/default_system/Default_Effect.txt"

#include "../util.dnh"

task CreateEnemyByShotAngle(x, y, shotAngle, health) {
    let objEnemy;
    let enemyX;
    let enemyY;
    let count = 0;
    let imgEnemy = GetCurrentScriptDirectory() ~ "../visuals/Robotcat.png";
    let deathSFX = GetCurrentScriptDirectory() ~ "../sfx/death.mp3";
    let damageSFX = GetCurrentScriptDirectory() ~ "../sfx/damage.wav";
    let damageSFXObj = createSFXObject(damageSFX, 100);
    let deathSFXObj = createSFXObject(deathSFX, 100);

    TInitialize; // Make the enemy

    task TInitialize {
        objEnemy = ObjEnemy_Create(OBJ_ENEMY);
        ObjEnemy_Regist(objEnemy);
        ObjEnemy_SetLife(objEnemy, health);

        ObjMove_SetPosition(objEnemy, x, y);

        // Track the location of the enemy. This enemy is static though, so this is unnecessary
        enemyX = x;
        enemyY = y;

        TMainLoop;
        TFinalize;
    }

    task TMainLoop {
        while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) {
            renderEnemy;
            damageEffect();

            // Enemy hitbox
            ObjEnemy_SetIntersectionCircleToShot(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 32); 
            ObjEnemy_SetIntersectionCircleToPlayer(objEnemy, ObjMove_GetX(objEnemy), ObjMove_GetY(objEnemy), 8);
        
            // Wait 30 frames before start shooting
            if (count == 30) {fireStraight;}
            count++;
            yield;
        }
    }

    task renderEnemy{
        ObjPrim_SetTexture(objEnemy, imgEnemy);
        ObjSprite2D_SetSourceRect(objEnemy, 1, 1, 84, 84);
        ObjSprite2D_SetDestCenter(objEnemy);  // This center no longer needs to be set for each new sprite?
        ObjRender_SetScaleXYZ(objEnemy, 1, 1, 0);

        while (!Obj_IsDeleted(objEnemy)){
            ObjSprite2D_SetSourceRect(objEnemy, 1, 1, 84, 84);
            yield;
        }
    }

    task fireStraight{
        let randRange = 10;
        while (ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) {
            CreateShotA1(enemyX, enemyY, 7, shotAngle + rand(-randRange, randRange), 76, 0);
            wait(5);
        }
    }

    function damageEffect(){
        if (ObjEnemy_GetInfo(objEnemy, INFO_SHOT_HIT_COUNT) > 0){
            playSFX(damageSFXObj);
        }
    }

    task TFinalize {
        while(ObjEnemy_GetInfo(objEnemy, INFO_LIFE) > 0) {yield;}
        playSFX(deathSFXObj);
        TExplosionA(x, y, 10, 0.1);
        screenshake(2,3);
        Obj_Delete(objEnemy);
    }
}