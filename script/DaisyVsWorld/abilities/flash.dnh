let flashSFX = GetCurrentScriptDirectory() ~ "../sfx/flash-sound.mp3";
let flashEnd = GetCurrentScriptDirectory() ~ "../visuals/flash-end.png";
let flashStart = GetCurrentScriptDirectory() ~ "../visuals/flash-start.png";

#include "./../util.dnh"

let playerObj = GetPlayerObjectID();
let initialX; // Store player location before flash
let initialY;
let flashSFXObj = createSFXObject(flashSFX, 100);

function blink(){
	let blinkDist = 100; // Specifies how many pixels the player blinks
	let playerDir = GetPlayerDir();
	initialX = GetPlayerX(); // Store player location before flash
	initialY = GetPlayerY();

	if (playerDir != -1){ //If we aren't trying to blink in place
		if (playerDir == 225){ //topleft
			ObjMove_SetPosition(playerObj, GetPlayerX() - blinkDist/1.414, GetPlayerY() - blinkDist/1.414);
		}
		else if (playerDir == 315){ //topright
			ObjMove_SetPosition(playerObj, GetPlayerX() + blinkDist/1.414, GetPlayerY() - blinkDist/1.414);
		}
		else if (playerDir == 135){ //botleft
			ObjMove_SetPosition(playerObj, GetPlayerX() - blinkDist/1.414, GetPlayerY() + blinkDist/1.414);
		}
		else if (playerDir == 45){ //botright
			ObjMove_SetPosition(playerObj, GetPlayerX() + blinkDist/1.414, GetPlayerY() + blinkDist/1.414);
		}
		else if(playerDir == 180){ //left
			ObjMove_SetPosition(playerObj, GetPlayerX() - blinkDist, GetPlayerY());
		}
		else if (playerDir == 0){ //right
			ObjMove_SetPosition(playerObj, GetPlayerX() + blinkDist, GetPlayerY());
		}
		else if (playerDir == 270){ //up
			ObjMove_SetPosition(playerObj, GetPlayerX(), GetPlayerY() - blinkDist);
		}
		else if (playerDir == 90){ //down
			ObjMove_SetPosition(playerObj, GetPlayerX(), GetPlayerY() + blinkDist);
		}
		flashStartAnimation; // Trigger the flash effect
		flashEndAnimation;
		SetPlayerInvincibilityFrame(20); // Iframes for after blink
		playSFX(flashSFXObj); // Calls util function to play sound effect
		screenshake(3, 8);
	}
}

task flashStartAnimation{
	let i = 0;
	let flashSize = 80;
	let flashStartObj = ObjPrim_Create(OBJ_SPRITE_2D);

	// Animation at initial location
	loop(3){
			i+=1;

			Obj_SetRenderPriorityI(flashStartObj, 100);
			ObjPrim_SetTexture(flashStartObj, flashStart);
			ObjSprite2D_SetSourceRect(flashStartObj, flashSize * (i-1), 0, flashSize * i, flashSize);
			ObjSprite2D_SetDestRect(flashStartObj, initialX - 40, initialY - 40, initialX + 40, initialY + 40);
			wait(3);
			yield;
		}
	Obj_Delete(flashStartObj);

}

task flashEndAnimation{
	let i = 0;
	let flashSize = 80;
	let flashEndObj = ObjPrim_Create(OBJ_SPRITE_2D);		

	// Animation at player destination
	loop(7){
		i+=1;

		Obj_SetRenderPriorityI(flashEndObj, 100);
		ObjPrim_SetTexture(flashEndObj, flashEnd);
		ObjSprite2D_SetSourceRect(flashEndObj, flashSize * (i-1), 0, flashSize * i, flashSize);
		ObjSprite2D_SetDestRect(flashEndObj, GetPlayerX() - 40, GetPlayerY() - 40, GetPlayerX() + 40, GetPlayerY() + 40);
		wait(3);
		yield;
	}
	Obj_Delete(flashEndObj);
}